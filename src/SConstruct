env = Environment(
    CCFLAGS=['-Wall', '-Wextra'],
    CPPPATH='.',
    LIBS=['GL', 'GLU']
    )

ragel = Builder(action='ragel $SOURCE -o $TARGET')
env.Append(BUILDERS={'Ragel': ragel})

flags = {
    'debug': { 'CCFLAGS': ['-O0', '-ggdb', '-Werror'] },
    'release': { 'CCFLAGS': ['-O2'] },
}

config = ARGUMENTS.get('config', 'debug')
env.MergeFlags(flags[config])
try:
    env['CXX'] = ARGUMENTS['CXX']
except KeyError:
    pass

sdlenv = env.Clone()
sdlenv.ParseConfig('sdl-config --cflags')

pcenv = env.Clone()
pcenv.ParseConfig('pkg-config --cflags pangocairo')

env.ParseConfig('pkg-config --libs pangocairo')
env.ParseConfig('sdl-config --libs')

env.Program('engine', [
    'game/space/entity.cpp',
    'game/space/gamescreen.cpp',
    'game/space/main.cpp',
    'game/space/player.cpp',
    'game/space/random.cpp',
    'game/space/scenery.cpp',
    'game/space/shapes.cpp',
    'game/space/ship.cpp',
    'game/space/shot.cpp',
    'game/space/stars.cpp',
    'game/space/thinker.cpp',
    'game/space/world.cpp',
    'game/tank/explosion.cpp',
    'game/tank/gamescreen.cpp',
    'game/tank/object.cpp',
    'game/tank/obstacle.cpp',
    'game/tank/player.cpp',
    'game/tank/shot.cpp',
    'game/tank/world.cpp',
    'graphics/dummytexture.cpp',
    'graphics/model.cpp',
    'graphics/texture.cpp',
    'graphics/texturefile.cpp',
    'graphics/video.cpp',
    sdlenv.Object('main.cpp'),
    'rand.cpp',
    'scene/group.cpp',
    'scene/leafobject.cpp',
    'scene/object.cpp',
    'sys/config.cpp',
    'sys/configfile.cpp',
    env.Ragel('sys/configfileread.cpp', 'sys/configfileread.rl'),
    'sys/ifile.cpp',
    'sys/ifilereg.cpp',
    #'sys/parseconfig.cpp',
    'sys/path.cpp',
    'sys/path_linux.cpp',
    #'sys/path_osx.cpp',
    'sys/path_xdg.cpp',
    'sys/resource.cpp',
    'sys/stringarray.cpp',
    'sys/system_error.cpp',
    #'sys/testconfig.cpp',
    #'type/font_osx.cpp',
    pcenv.Object('type/font_pango.cpp'),
    'type/rastertext.cpp',
    pcenv.Object('type/rastertext_cairo.cpp'),
    #'type/rastertext_osx.cpp',
    'ui/button.cpp',
    'ui/menu.cpp',
    'ui/mousemanager.cpp',
    'ui/screen.cpp',
    'ui/widget.cpp',
    ])
